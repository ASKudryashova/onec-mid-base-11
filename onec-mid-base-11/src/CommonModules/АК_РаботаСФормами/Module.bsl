
#Область ПрограммныйИнтерфейс

//Проверяет имена форм документов и вызывает процедуры внесения изменений в соответствующие формы
//
//Параметры:
//	Форма - Форма - форма документа, создаваемая на сервере
//
// Пример:
//	АК_РаботаСФормами.ДополнитьФорму(Форма)
//
Процедура ДополнитьФорму(Форма) Экспорт
	
	ИмяФормы = Форма.ИмяФормы;
	
	Если ИмяФормы = "Документ.ЗаказПокупателя.Форма.ФормаДокумента" ИЛИ
		ИмяФормы = "Документ.ПоступлениеТоваровУслуг.Форма.ФормаДокумента" ИЛИ
		ИмяФормы = "Документ.РеализацияТоваровУслуг.Форма.ФормаДокумента" Тогда
		ДобавитьПолеКонтактноеЛицоВГруппаШапкаЛево(Форма);
	ИначеЕсли ИмяФормы = "Документ.ОплатаОтПокупателя.Форма.ФормаДокумента" ИЛИ
		ИмяФормы = "Документ.ОплатаПоставщику.Форма.ФормаДокумента" Тогда
		ВставитьПолеКонтактноеЛицоНаФормуПередСуммаДокумента(Форма);
	КонецЕсли;
	
	Если ИмяФормы = "Документ.ЗаказПокупателя.Форма.ФормаДокумента" Тогда 
		
		ДобавитьГруппуСкидки(Форма);
		
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Процедура ДобавитьПолеКонтактноеЛицоВГруппаШапкаЛево(Форма)
	
	ПолеВводаКонтактноеЛицо = Форма.Элементы.Добавить("КонтактноеЛицо", Тип("ПолеФормы"), Форма.Элементы.ГруппаШапкаЛево);
	ПолеВводаКонтактноеЛицо.Вид = ВидПоляФормы.ПолеВвода;
	ПолеВводаКонтактноеЛицо.ПутьКДанным = "Объект.АК_КонтактноеЛицо";
	
КонецПроцедуры

Процедура ВставитьПолеКонтактноеЛицоНаФормуПередСуммаДокумента(Форма)
	
	ПолеВводаКонтактноеЛицо = Форма.Элементы.Вставить("КонтактноеЛицо", Тип("ПолеФормы"),, Форма.Элементы.СуммаДокумента);
	ПолеВводаКонтактноеЛицо.Вид = ВидПоляФормы.ПолеВвода;
	ПолеВводаКонтактноеЛицо.ПутьКДанным = "Объект.АК_КонтактноеЛицо";
	
КонецПроцедуры

Процедура ДобавитьГруппуСкидки(Форма);
	
	ГруппаФормыСкидка = Форма.Элементы.Вставить("ГруппаСкидка", Тип("ГруппаФормы"),, Форма.Элементы.Страницы);
	ГруппаФормыСкидка.Вид = ВидГруппыФормы.ОбычнаяГруппа;
	ГруппаФормыСкидка.Отображение = ОтображениеОбычнойГруппы.Нет;
	ГруппаФормыСкидка.Группировка = ГруппировкаПодчиненныхЭлементовФормы.ГоризонтальнаяВсегда;
	
	ПолеВводаСкидки = Форма.Элементы.Добавить("СогласованнаяСкидка", Тип("ПолеФормы"), Форма.Элементы.ГруппаСкидка);
	ПолеВводаСкидки.Вид = ВидПоляФормы.ПолеВвода;
	ПолеВводаСкидки.ПутьКДанным = "Объект.АК_СогласованнаяСкидка";
	ПолеВводаСкидки.УстановитьДействие("ПриИзменении", "АК_СогласованнаяСкидкаПриИзменении");
	
	КомандаПересчитатьСоСкидкой = Форма.Команды.Добавить("ПересчитатьСоСкидкой");
	КомандаПересчитатьСоСкидкой.Заголовок = "Пересчитать со скидкой";
	КомандаПересчитатьСоСкидкой.Действие = "КомандаПересчитатьСоСкидкой";
	
	КнопкаПересчитатьСоСкидкой = Форма.Элементы.Добавить("ПересчитатьСоСкидкой", Тип("КнопкаФормы"), Форма.Элементы.ГруппаСкидка);
	КнопкаПересчитатьСоСкидкой.ИмяКоманды = "ПересчитатьСоСкидкой";
	КнопкаПересчитатьСоСкидкой.Вид = ВидКнопкиФормы.ОбычнаяКнопка;

КонецПроцедуры

#КонецОбласти